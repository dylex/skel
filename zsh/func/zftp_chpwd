# function zftp_chpwd {

# If the directory really changed...
if [[ $ZFTP_PWD != $zfconfig[lastdir_$ZFTP_SESSION] ]]; then
  # ...and also empty the stored directory listing cache.
  # As this function is called when we close the connection, this
  # is the only place we need to do these two things.
  local curdir=$zfconfig[curdir_$ZFTP_SESSION]
  [[ -n $curdir && -f $curdir ]] && rm -f $curdir
  zfconfig[otherargs_$ZFTP_SESSION]=
  zffcache -d
  psvar=(${(@)psvar[2,$]})
fi

if [[ -z $ZFTP_USER ]]; then
  # last call, after an FTP logout

  # delete the non-current cached directory
  [[ -n $zfotherdir && -f $zfotherdir ]] && rm -f $zfotherdir

  # don't keep lastdir between opens (do keep lastloc)
  zfconfig[lastdir_$ZFTP_SESSION]=
else
  [[ -n $ZFTP_PWD ]] && zfconfig[lastdir_$ZFTP_SESSION]=$ZFTP_PWD
  zfconfig[lastloc_$ZFTP_SESSION]="$ZFTP_HOST:$ZFTP_PWD"
  zfconfig[lastuser_$ZFTP_SESSION]="$ZFTP_USER"
  psvar=($zfconfig[lastloc_$ZFTP_SESSION] ${psvar[@]})
fi
# }
